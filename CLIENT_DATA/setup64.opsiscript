[Actions]

; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	if FileExists("%ScriptPath%\delsub64.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub64.opsiscript"
	endif

	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."
	comment "Start setup program"
	Winbatch_install_msi
	Sub_check_exitcode

	comment "Test for installation success"
	Set $SearchPattern$ = $ProductName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge

	if ( count ($ResultList$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
		isFatalError
	else
		Set $RegId$ = takeSTring(0, $ResultList$)
		; Leider setzt das MSI keine 'InstallLocation' bzw. 'DisplayIcon' muss also manuell hier gemacht werden.
		; Damit es für eine spätere Deinstallation zur Verfügung steht, wird es in die Registry geschrieben
		comment "Patch missing registry keys like 'InstallLocation' and 'DisplayIcon'"
		Registry_install /64Bit

		; MSI legt standardmäßig ein Desktopicon an, keinen einfachen Weg gefunden, das zu unterbinden. Name des Icons lautet wie "DisplayName"
		set $DesktopLink$ = getProductProperty("desktoplink","false")
		if $DesktopLink$ = "false"
			comment "Delete common desktop icon generated by MSI"
			Set $DisplayName$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayName")
			Linkfolder_delete_desktoplink	
		endif

		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
			endif
		endif
	endif
endif

[Winbatch_install_msi]
msiexec $MsiSilentOption$ /i "%ScriptPath%\$InstFile$" /l* "$LogDir$\$ProductId$.install_log.txt" ALLUSERS=1 REBOOT=ReallySuppress

[Registry_install]
openkey [$RegPathUninstall$\$RegId$]
set "InstallLocation" = REG_SZ:"$InstallDir$"
set "DisplayIcon" = REG_SZ:"$PathToExe$"

[Linkfolder_delete_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $DisplayName$

